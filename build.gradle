plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'CodeMC'
        url = 'https://repo.codemc.org/repository/maven-public'
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'org.spigotmc:spigot-api:1.16.3-R0.1-SNAPSHOT'
    implementation 'io.github.bananapuncher714:NBTEditor:7.16'
    shadow group: 'io.github.bananapuncher714', name: 'NBTEditor', version: '7.16'
}


import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.taskdefs.condition.Os

import java.nio.file.Paths

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

task deletePlugin {
    String dir = Os.isFamily(Os.FAMILY_WINDOWS) ? winServerDir : linServerDir
    logger.info("Spigot server directory: $dir")
    def plugins = Paths.get(dir).resolve("plugins").toFile().listFiles() ?: new File[0]
    logger.debug("Plugin list size: ${plugins.length}")
    iterateAndDelete plugins
}

task deployPlugin(type: Copy) {
    String dir = Os.isFamily(Os.FAMILY_WINDOWS) ? winServerDir : linServerDir
    String pluginDir = Paths.get(dir).resolve("plugins").toAbsolutePath().toString()
    String artifactName = rootProject.name + "-" + version + "-all.jar"
    def pluginPath = "$buildDir/libs/$artifactName"
    from pluginPath
    into pluginDir
    doLast {
        logger.info("Deployed $artifactName")
    }
}

deployPlugin.dependsOn deletePlugin, shadowJar

void iterateAndDelete(File[] plugins) {
    plugins.each { File item ->
        logger.debug("Inspecting ${item.getName()}")
        if (item.name.startsWith(rootProject.name)) {
            def name = item.name
            item.delete()
            logger.info("Removed $name")
        }
    }
}
